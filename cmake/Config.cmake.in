# SPDX-License-Identifier: Apache-2.0
# Copyright 2013-2022 The Foundry Visionmongers Ltd

set(@PROJECT_NAME@_VERSION @PROJECT_VERSION@)

@PACKAGE_INIT@

# TODO(DF): add here any required find_dependency(), E.g.
# include(CMakeFindDependencyMacro)
# find_dependency(Imath @Imath_VERSION@ HINTS @Imath_DIR@)

set_and_check (@PROJECT_NAME@_DIR "${PACKAGE_PREFIX_DIR}")
if (WIN32 AND @BUILD_SHARED_LIBS@)
    # For .dll files.
    set_and_check (@PROJECT_NAME@_BINARY_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@")
endif ()
set_and_check (@PROJECT_NAME@_INCLUDE_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@")
set_and_check (@PROJECT_NAME@_LIBRARY_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@")

if (@OPENASSETIO_ENABLE_PYTHON@)
    set(@PROJECT_NAME@_Python_VERSION "@Python_VERSION@")
    set(@PROJECT_NAME@_Python_VERSION_MAJOR "@Python_VERSION_MAJOR@")
    set(@PROJECT_NAME@_Python_VERSION_MINOR "@Python_VERSION_MINOR@")
    set(@PROJECT_NAME@_Python_VERSION_PATCH "@Python_VERSION_PATCH@")
    set_and_check (
        @PROJECT_NAME@_Python_SITELIB
        "${PACKAGE_PREFIX_DIR}/@OPENASSETIO_PYTHON_SITEDIR@")
endif ()

include ("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")

check_required_components ("@PROJECT_NAME@")
